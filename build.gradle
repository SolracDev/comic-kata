apply plugin: 'java'
apply plugin: 'groovy'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['--plugin', 'pretty', '--glue', 'src/test/acceptance', 'src/test/resources']

        }
    }
}

dependencies {


    compile 'org.codehaus.groovy:groovy-all:2.4.0'

    cucumberRuntime files("${jar.archivePath}")

    testCompile 'junit:junit:4.11'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile "info.cukes:cucumber-junit:1.2.2"
    testCompile "info.cukes:cucumber-groovy:1.2.2"
}

def languageLevel = 1.8

sourceSets {
    sourceCompatibility = languageLevel
    targetCompatibility = languageLevel

    main.groovy.srcDirs = ['src/main/groovy']
    test.groovy.srcDirs = ['src/test/unit', 'src/test/integration', 'src/test/acceptance']
}